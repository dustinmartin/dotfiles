" ---------------------------------------------------------------------------
" Vundle
" ---------------------------------------------------------------------------

filetype off                   " required!

set rtp+=~/.vim/bundle/vundle/
call vundle#rc()

" Let Vundle manage Vundle
Bundle 'gmarik/vundle'

" Install other bundles
Bundle 'vim-scripts/The-NERD-tree'
Bundle 'tpope/vim-fugitive'
Bundle 'tpope/vim-markdown'
Bundle 'tpope/vim-surround'
Bundle 'tpope/vim-haml'
Bundle 'tpope/vim-unimpaired'
Bundle 'Lokaltog/vim-easymotion'
Bundle 'rking/ag.vim'
Bundle 'godlygeek/tabular'
Bundle 'SirVer/ultisnips'
Bundle 'ervandew/supertab'
Bundle 'mattn/emmet-vim'
Bundle 'scrooloose/syntastic'
Bundle 'tomtom/tcomment_vim'
Bundle 'kien/ctrlp.vim'
Bundle 'Raimondi/delimitMate'
Bundle 'Lokaltog/vim-powerline'
Bundle 'myusuf3/numbers.vim'
Bundle 'hail2u/vim-css3-syntax'
Bundle 'airblade/vim-gitgutter'
Bundle 'terryma/vim-multiple-cursors'
Bundle 'othree/javascript-libraries-syntax.vim'
Bundle 'jelera/vim-javascript-syntax'
Bundle 'pangloss/vim-javascript'
" Bundle 'marijnh/tern_for_vim'
Bundle 'Valloric/YouCompleteMe'
Bundle 'Valloric/MatchTagAlways'
Bundle 'jceb/vim-orgmode'
Bundle 'chrisbra/NrrwRgn'

" Color schemes
Bundle 'tomasr/molokai'
Bundle 'nanotech/jellybeans.vim'

" ---------------------------------------------------------------------------
" General
" ---------------------------------------------------------------------------

syntax on 						" Enable syntax highlighting
filetype plugin indent on 		" Detect and handle filetypes
set nocompatible 				" Don't be compatible with Vi

" ---------------------------------------------------------------------------
" Colors / Theme
" ---------------------------------------------------------------------------

" This is overwritten by the gui options
colorscheme jellybeans

" ---------------------------------------------------------------------------
" Files
" ---------------------------------------------------------------------------

set fileencoding=utf-8
set fileformat=unix
set fileformats=unix,mac,dos

" ---------------------------------------------------------------------------
" Settings
" ---------------------------------------------------------------------------

set hidden 						" Allow unsaved buffers to be hidden
set nowrap						" Turn word wrapping off. :set wrap turns it back on.
set tabpagemax=50 				" Increase the number of allowed tabs
set showtabline=2 				" Always show the tabline
set ruler						" Turn on row,column dislay on status bar
set backspace=2           		" Allow backspacing over everything in insert mode
set whichwrap+=<,>,h,l,[,] 		" Backspace and cursor keys wrap too
set visualbell 					" Disable 'beep' for wrong commands and do screen flash
set cursorline 					" Highlights the cursor line
set ttyfast 					" Makes scrolling smoother
set laststatus=2 				" Always show the statusline
set textwidth=0 				" Prevent Vim from wrapping lines
set wrapmargin=0 				" Prevent Vim from wrapping lines
set sidescroll=1 				" Number of columns to scroll horizontally
set sidescrolloff=10            " Number of columns to keep to the left and right of cursor
set scrolloff=1					" Number of rows to keep above/below the cursor
set mouse=a						" Enable mouse
set showcmd						" Show command in bottom right of window
set clipboard=unnamed			" Default to the system clipboard
set showmode 					" Don't show the current Vim mode
set lazyredraw  				" Don't update the display while executing macros
set wrapscan 					" Set the search scan to wrap around the file
set virtualedit=block 			" Allow the cursor to go in to invalid places in visual block mode
set shortmess+=I 				" Skip the splash screen
set wildmenu 					" Command completion with tab
set matchtime=3					" How many tenths of a second to highlight a matching parentheses
set spelllang=en_us 			" Set the spell check dictionary
set number
set shiftwidth=4 				" Number of spaces to use for each step of (auto)indent
set tabstop=4 					" Number of spaces that a <Tab> in the file counts for
set autoindent 					" Copy the indent from the current line when starting a new line
set nosmartindent
set nocindent
set linespace=2
set switchbuf=usetab,newtab 	" Open new buffers always in new tabs
set splitright                  " Split vertical windows right to the current windows
set splitbelow                  " Split horizontal windows below to the current windows
set autoread                    " Automatically reread changed files without asking me anything
let mapleader = ","				" Set leader key to comma

set complete-=i

if &encoding ==# 'latin1' && has('gui_running')
    set encoding=utf-8
endif

"  ---------------------------------------------------------------------------
"  Status Line
"  ---------------------------------------------------------------------------

" Filename
set statusline=\ [%t]

" Modified Flag
set statusline+=\ %m

" Preview Screen Flag
set statusline+=%w

" Left / Right Alignment Separater 
set statusline+=%=

" Encoding
set statusline+=\ [%{strlen(&fenc)?&fenc:&enc}]

" Line x of y
set statusline+=\ [Line\ %l\/%L]

" Percentage Through File
set statusline+=\ [%P]  

" Extra space
set statusline+=\  

" ---------------------------------------------------------------------------
" GUI/MacVim Options
" ---------------------------------------------------------------------------

if has("gui_running")
    " Set the tab labels
    set guitablabel=%t\ %m

    " Hide the toolbar
    set guioptions-=T 

    " Set the font
    " set guifont=Ubuntu\ Mono\ for\ Powerline:h18 " Set the font style and size
    set guifont=Ubuntu\ Mono:h18 " Set the font style and size

    " Window size
    set lines=40 columns=130 	 " Set the window size

    " Disable the scrollbars
    set guioptions-=L			 " Disable left scrollbar
    set guioptions-=r			 " Disable right scrollbar

    " Colorscheme
    colorscheme molokai
endif

" ---------------------------------------------------------------------------
" Splits 
" ---------------------------------------------------------------------------

" Open a new split and switch to it
noremap <leader>vs <C-w>v<C-w>l
noremap <leader>hs <C-w>s<C-w>j

" Move around the splits
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" ---------------------------------------------------------------------------
" Searching
" ---------------------------------------------------------------------------

set ignorecase 							" Makes searches case insensitive if search string is all lower case
set smartcase 							" Makes searches case SENSITIVE if search string contains an uppercase letter
set gdefault                    		" Search/replace 'globally' (on a line) by default
set incsearch 							" Start searching before pressing enter
set showmatch 							" Highlight the matching bracket when one is inserted
set hlsearch 							" Highlight search results (as you type)

" Use standard regexes
nnoremap / /\v
vnoremap / /\v

" ----------------------------------------------------------------------------
" Backups / Swap / Undo Files
" ----------------------------------------------------------------------------

set noswapfile

set nobackup 								" Disable the creation of backup files
" set backupcopy=yes              			" Keep attributes of original file
" set backupdir=~/.vim/backup//  			" Store backups under ~/.vim/backup

" set undofile 								" Enable undo files
set undolevels=1000 						" The number of undo levels to allow
" set undodir=~/.vim/undo// 				" Set the undo directory location

" ----------------------------------------------------------------------------
" Sessions
" ----------------------------------------------------------------------------

" What parts of the session should be saved?
set sessionoptions=curdir,tabpages,localoptions,folds

function! RestoreSession()
	if argc() == 0 "vim called without arguments
		execute 'source ~/.vim/session'
	end
endfunction

function! SaveSession()
	if argc() == 0 "vim called without arguments
		execute 'mksession! ~/.vim/session'
	end
endfunction

autocmd VimEnter * call RestoreSession()
autocmd VimLeave * call SaveSession()

" -----------------------------------------------------------
"  Tabs
" -----------------------------------------------------------

" Open a new tab
map <leader>t :tabnew<CR>

" -----------------------------------------------------------
"  Tabline
" -----------------------------------------------------------

set tabline=%!MyTabLine()

function! MyTabLine()
	let s = ''

	for i in range(tabpagenr('$'))
		" select the highlighting
		if i + 1 == tabpagenr()
			let s .= '%#TabLineSel#'
		else
			let s .= '%#TabLine#'
		endif

		" set the tab page number (for mouse clicks)
		let s .= '%' . (i + 1) . 'T' 

		let tabNum = i + 1
		let s .= ' '. tabNum . ') '

		" the label is made by MyTabLabel()
		let s .= ' %{MyTabLabel(' . (i + 1) . ')}'
	endfor			

	" after the last tab fill with TabLineFill and reset tab page nr
	let s .= '%#TabLineFill#%T'

	" right-align the label to close the current tab page
	if tabpagenr('$') > 1 
		let s .= '%=%#TabLine#%999X[close]'
	endif

	return s
endfunction

function! MyTabLabel(n)
	let buflist = tabpagebuflist(a:n)
	let winnr = tabpagewinnr(a:n)
	
	let label  = bufname(buflist[winnr - 1]) 

	if label == ''
		let label = 'Untitled'
	endif

	let label .= (getbufvar(buflist[winnr - 1], "&mod")?' + ':'   ')

	return fnamemodify(label, ":t") 
endfunction

hi TabLineFill ctermfg=LightGrey ctermbg=Black
hi TabLine ctermfg=LightGrey ctermbg=Black
hi TabLineSel term=underline ctermfg=LightGrey ctermbg=DarkGrey

" -----------------------------------------------------------
" Misc Mappings
" -----------------------------------------------------------

" Yank till end of line
nnoremap Y y$ 

" Toggle spell checking
nnoremap <Leader>sp :set spell!<CR>

" Open the scratchpad
map <Leader>sc :tabedit ~/Dropbox/Notes/scratchpad.md<CR>

" Toggle word wrapping (won't wrap in the middle of a word)
nnoremap <leader>ww :set wrap! linebreak nolist<CR>

" U does some weird stuff. Remap to u
nnoremap U u
vnoremap U u

" Prevent vim from unindenting on blank lines
" inoremap <cr> <cr>x<bs>
" nnoremap o ox<BS>
" nnoremap O Ox<BS>

" Preserve indentation while pasting text from the OS X clipboard
nnoremap <leader>p :set paste<CR>:put  *<CR>:set nopaste<CR>

" Close panel/window
map <leader>x :close<cr>

"Shortcut for editing  vimrc file in a new tab
nmap <leader>ev :tabedit $MYVIMRC<cr>
nmap <leader>sv :source $MYVIMRC<cr>

" Map tab to indent
vnoremap <Tab> >gv
nnoremap <Tab> >>

" Bubble single lines
nmap <C-Up> [e
nmap <C-Down> ]e
imap <C-Up> <esc>[e
imap <C-Down> <esc>]e

" Bubble multiple lines
vmap <C-Up> [egv
vmap <C-Down> ]egv

" Map shift+tab to unindent
vnoremap <S-Tab> <gv
nnoremap <S-Tab> <<

" Keep a block selected after indenting
vnoremap < <gv
vnoremap > >gv

" Visually select the text that was last edited/pasted
nmap gV `[v`]

" Map ESC
imap jj <ESC>

" -----------------------------------------------------------
" Movement
" -----------------------------------------------------------

" Movement by screen line instead of file line
nnoremap j gj
nnoremap k gk

nnoremap <down> gj
nnoremap <up> gk

" Allow mode selection vs shift-j (J) and shift-k (K)
nnoremap J V gj
nnoremap K V gk
vnoremap J gj
vnoremap K gk

" Remap ; to :
vnoremap ; :
nnoremap ; :

" -----------------------------------------------------------
" Code Folding
" -----------------------------------------------------------

set foldenable 							" Enable folding
set foldcolumn=0 						" Don't add a fold column
set foldlevel=99            			" Start out with nothing folded
set foldlevelstart=99            		" Start out with nothing folded
set foldmethod=syntax

" What actions open a fold?
set foldopen=block,hor,insert,jump,mark,percent,quickfix,search,tag,undo

" Fold the {} block - for manual folding only
" nnoremap zb vi{zf 

" Refocus folds
nnoremap ,z zMzvzz

" Make zO recursively open whatever top level fold we're in, no matter where the cursor happens to be.
nnoremap zO zCzO

function! CustomFoldText()

    "get first non-blank line
    let fs = v:foldstart

    while getline(fs) =~ '^\s*$' | let fs = nextnonblank(fs + 1)
    endwhile

    if fs > v:foldend
        let line = getline(v:foldstart)
    else
        let line = substitute(getline(fs), '\t', repeat(' ', &tabstop), 'g')
    endif

    let w = winwidth(0) - &foldcolumn - (&number ? 8 : 0)
    let foldSize = 1 + v:foldend - v:foldstart
    let foldSizeStr = " " . foldSize . " lines "
    let foldLevelStr = repeat("+--", v:foldlevel)
    let lineCount = line("$")
    let foldPercentage = printf("[%.1f", (foldSize*1.0)/lineCount*100) . "%] "
    let expansionString = repeat(" ", w - strwidth(foldSizeStr.line.foldLevelStr.foldPercentage))

    return line . expansionString . foldSizeStr . foldPercentage . foldLevelStr

endfunction

set foldtext=CustomFoldText()

" -----------------------------------------------------------
" Emmet 
" -----------------------------------------------------------

let g:user_emmet_expandabbr_key = '<c-z>'

" -----------------------------------------------------------
" NerdTree
" -----------------------------------------------------------

" Toggle nerdtree
vnoremap <leader>nt <ESC> :NERDTreeToggle<CR>
nnoremap <leader>nt :NERDTreeToggle<CR>

" Show the bookmarks table on startup
let NERDTreeShowBookmarks=1

" Show hidden files, too
let NERDTreeShowFiles=1

" Quit on opening files from the tree
let NERDTreeQuitOnOpen=0

" Highlight the selected entry in the tree
let NERDTreeHighlightCursorline=1

" Use a single click to fold/unfold directories and a double click to open files
let NERDTreeMouseMode=2

" Allow NerdTree to change Vim's CD
let NERDTreeChDirMode=2

" -----------------------------------------------------------
" Syntastic 
" -----------------------------------------------------------

let g:syntastic_check_on_open=1
" let g:syntastic_enable_signs=0

" -----------------------------------------------------------
" CtrlP
" -----------------------------------------------------------

inoremap <C-p> <ESC>:CtrlP<CR>
vnoremap <C-p> <ESC>:CtrlP<CR>

nnoremap <leader>b :CtrlPBuffer<CR>
nnoremap <leader>f :CtrlP<CR>

" Search by filename
let g:ctrlp_by_filename = 1
let g:ctrlp_tabpage_position= 'al'
let g:ctrlp_cmd = 'CtrlP'
let g:ctrlp_custom_ignore = {
	\ 'dir':  '\v[\/]\.(git|hg|svn)$',
	\ 'file': '\v\.(jpg|jpeg|png|gif|zip|pdf|dmg)$'
	\ }
let g:ctrlp_prompt_mappings = {
    \ 'AcceptSelection("e")': ['<c-t>'],
    \ 'AcceptSelection("t")': ['<cr>', '<2-LeftMouse>'],
    \ }

let g:ctrlp_working_path_mode = 0
let g:ctrlp_clear_cache_on_exit = 0

" -----------------------------------------------------------
" Powerline
" -----------------------------------------------------------

let g:Powerline_symbols = 'compatible'

" -----------------------------------------------------------
" Tabular
" -----------------------------------------------------------

nmap <Leader>a= :Tabularize /=<CR>
vmap <Leader>a= :Tabularize /=<CR>

nmap <Leader>a: :Tabularize /:<CR>
vmap <Leader>a: :Tabularize /:<CR>

" -----------------------------------------------------------
" UltiSnips
" -----------------------------------------------------------

let g:UltiSnipsExpandTrigger="<c-space>"
let g:UltiSnipsListSnippets="<c-s-space>"
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsJumpBackwardTrigger="<c-k>"

" -----------------------------------------------------------
" Syntax Specific
" -----------------------------------------------------------

" Don't highlight HTML links
:hi link htmlLink NONE<CR>

" -----------------------------------------------------------
" Silver Searcher / Ack
" -----------------------------------------------------------
  
nnoremap <leader>a :Ag!<space>

" -----------------------------------------------------------
" Javascript Lib Syntax
" -----------------------------------------------------------

let g:used_javascript_libs = 'angularjs,jquery,underscore'
